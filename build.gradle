buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE'
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'application'
apply plugin: 'spring-boot'

group 'me.loki2302'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '2.5'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

mainClassName = 'me.loki2302.App'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.springframework.boot:spring-boot-starter-test:1.3.0.RELEASE'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.0.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-web:1.3.0.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.3.0.RELEASE'
    compile 'io.springfox:springfox-swagger2:2.2.2'
    compile 'io.springfox:springfox-swagger-ui:2.2.2'
    compile 'org.hibernate:hibernate-validator:5.2.2.Final'
    compile 'com.h2database:h2:1.4.190'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
    outputs.dir snippetsDir
}

asciidoctor {
    sourceDir 'src/main/asciidoc'
    sources {
        include 'restdocs.adoc'
    }
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'resources/'
        include 'restdocs.html'
    }
}
